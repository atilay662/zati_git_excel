CLASS lhc_User DEFINITION INHERITING FROM cl_abap_behavior_handler.
  PRIVATE SECTION.

    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING keys REQUEST requested_authorizations FOR User RESULT result.
    METHODS get_global_authorizations FOR GLOBAL AUTHORIZATION
      IMPORTING REQUEST requested_authorizations FOR User RESULT result.
    METHODS uploadExcelData FOR MODIFY
      IMPORTING keys FOR ACTION User~uploadExcelData RESULT result.
    METHODS get_instance_features FOR INSTANCE FEATURES
      IMPORTING keys REQUEST requested_features FOR User RESULT result.
    METHODS fillselectedstatus FOR DETERMINE ON MODIFY
      IMPORTING keys FOR user~fillselectedstatus.
    METHODS fillfilestatus FOR DETERMINE ON MODIFY
      IMPORTING keys FOR user~fillfilestatus.
    METHODS downloadexcel FOR MODIFY
      IMPORTING keys FOR ACTION user~downloadexcel RESULT result.
    " User defined method
    METHODS read_excel IMPORTING im_get_template TYPE abap_boolean
                                 im_get_header   TYPE abap_boolean OPTIONAL
                       EXPORTING et_excel_data   TYPE ANY TABLE
                       CHANGING  ch_attachment   TYPE zt2123_user-attachment.

ENDCLASS.



CLASS lhc_User IMPLEMENTATION.

  METHOD get_instance_authorizations.

  ENDMETHOD.

  METHOD get_global_authorizations.
  ENDMETHOD.

  METHOD uploadExcelData.
    DATA: lo_table_descr        TYPE REF TO cl_abap_tabledescr,
          lo_struct_descr       TYPE REF TO cl_abap_structdescr,
          lt_excel_user         TYPE STANDARD TABLE OF zbp_i_ati_user=>ty_exl_file,
          lt_excel_user_new     TYPE STANDARD TABLE OF zbp_i_ati_user=>ty_exl_file,
          lt_excel_userdev_old  TYPE STANDARD TABLE OF zbp_i_ati_user=>ty_exl_file,
          lt_excel_userdev      TYPE STANDARD TABLE OF zbp_i_ati_user=>ty_exl_file,
          lt_excel_temp         TYPE STANDARD TABLE OF zbp_i_ati_user=>ty_exl_file,
          lt_excel_filter       TYPE SORTED TABLE OF zbp_i_ati_user=>ty_exl_file WITH UNIQUE KEY emp_id dev_id,
          lt_data_user_dev      TYPE TABLE FOR CREATE zi_ati_user\_UserDev,
          lt_data_user          TYPE TABLE FOR CREATE zi_ati_user,
          lt_user_key           TYPE TABLE FOR READ IMPORT zi_ati_user,
          lv_tabix              TYPE sy-tabix,
          lv_index              TYPE sy-index,
          lv_userdev_new_record TYPE int1.


    DATA(lv_user) = cl_abap_context_info=>get_user_technical_name( ).

    READ ENTITIES OF zi_ati_user IN LOCAL MODE
    ENTITY User
    ALL FIELDS WITH CORRESPONDING #( keys )
    RESULT DATA(lt_file_entity).

    DATA(lv_attachment) = lt_file_entity[ 1 ]-attachment.
    CHECK lv_attachment IS NOT INITIAL.

    "Get excel data into internal table
    me->read_excel(
      EXPORTING
        im_get_template = ''
        im_get_header = 'X'
      IMPORTING
        et_excel_data   = lt_excel_temp
      CHANGING
        ch_attachment   = lv_attachment
    ).

    "Validating  if the template is valid one or not

*    " Get number of columns in upload file for validation
    TRY.
        lo_table_descr ?= cl_abap_tabledescr=>describe_by_data( p_data = lt_excel_temp ).
        lo_struct_descr ?= lo_table_descr->get_table_line_type( ).
        DATA(lv_no_of_cols) = lines( lo_struct_descr->components ).
      CATCH cx_sy_move_cast_error.
        "Implement error handling
    ENDTRY.

    "Validate Header record
    DATA(ls_excel) = VALUE #( lt_excel_temp[ 1 ] OPTIONAL ).
    IF ls_excel IS NOT INITIAL.
      DO lv_no_of_cols TIMES.
        lv_index = sy-index.
        ASSIGN COMPONENT lv_index OF STRUCTURE ls_excel TO FIELD-SYMBOL(<lfs_col_header>).
        CHECK <lfs_col_header> IS ASSIGNED.
        DATA(lv_value) =  to_upper(  <lfs_col_header> ) .
        DATA(lv_has_error) = abap_false.
        CASE lv_index.
          WHEN 1.
            lv_has_error = COND #( WHEN lv_value <> 'USER ID' THEN abap_true ELSE lv_has_error ).
          WHEN 2.
            lv_has_error = COND #( WHEN lv_value <> 'DEVELOPMENT ID' THEN abap_true ELSE lv_has_error ).
          WHEN 3.
            lv_has_error = COND #( WHEN lv_value <> 'DEVELOPMENT DESCRIPTION' THEN abap_true ELSE lv_has_error ).
          WHEN 4.
            lv_has_error = COND #( WHEN lv_value <> 'OBJECT TYPE' THEN abap_true ELSE lv_has_error ).
          WHEN 5.
            lv_has_error = COND #( WHEN lv_value <> 'OBJECT NAME' THEN abap_true ELSE lv_has_error ).
          WHEN 9. "More than 7 columns (error)
            lv_has_error = abap_true.
        ENDCASE.
        IF lv_has_error = abap_true.
          APPEND VALUE #( %tky = lt_file_entity[ 1 ]-%tky ) TO failed-user.
          APPEND VALUE #(
            %tky = lt_file_entity[ 1 ]-%tky
            %msg = new_message_with_text(
                     severity = if_abap_behv_message=>severity-error
                     text     = 'Incorrect Excel Template.' )
          ) TO reported-user.
          UNASSIGN <lfs_col_header>.
          EXIT.
        ENDIF.
        UNASSIGN <lfs_col_header>.
      ENDDO.
    ENDIF.
    CHECK lv_has_error = abap_false.

    "1st Version of the app.
    "----------------------------------------------------------------------
    DELETE lt_excel_temp INDEX 1.
    DELETE lt_excel_temp WHERE emp_id  IS INITIAL AND dev_id IS INITIAL.

    " Filter with current dev id details
    lt_excel_filter = VALUE #( ( emp_id = keys[ 1 ]-EmpId dev_id = keys[ 1 ]-DevId ) ).
*    " excel data with  current dev id details
    lt_excel_userdev = FILTER #( lt_excel_temp IN lt_excel_filter WHERE emp_id = emp_id AND dev_id = dev_id ).
    IF lt_excel_userdev IS  INITIAL.

      reported = VALUE #( BASE reported user = VALUE #( ( %tky = keys[ 1 ]-%tky
                                                                        %msg = new_message_with_text( severity =
                                                                        if_abap_behv_message=>severity-error
                                                                        text = 'Invalid Entry in Excel.' )
                                                                      ) )  ).
    ELSE.


**      "Fill serial number
      LOOP AT lt_excel_userdev ASSIGNING FIELD-SYMBOL(<lfs_excel>).
        <lfs_excel>-serial_no = sy-tabix.
      ENDLOOP.
*
*      "Prepare Data for  Child Entity (UserDev)
      lt_data_user_dev = VALUE #(
          (   %cid_ref  = keys[ 1 ]-%cid_ref
              EmpId   = keys[ 1 ]-EmpId
              DevId    = keys[ 1 ]-DevId
              %target   = VALUE #(
                               FOR lwa_excel IN lt_excel_userdev (
                                    %cid         = keys[ 1 ]-%cid_ref
                                    %data = VALUE #(
                                                     EmpId = keys[ 1 ]-EmpId
                                                     DevId = keys[ 1 ]-DevId
                                                     SerialNo = lwa_excel-serial_no
                                                     ObjectType = lwa_excel-obj_type
                                                     ObjectName = lwa_excel-obj_name
                                                  )
                                     %control = VALUE #(
                                                     EmpId = if_abap_behv=>mk-on
                                                     DevId = if_abap_behv=>mk-on
                                                     SerialNo = if_abap_behv=>mk-on
                                                     ObjectType = if_abap_behv=>mk-on
                                                     ObjectName = if_abap_behv=>mk-on

                                        )
                      ) ) ) ).

      "Delete Existing entry for user if any
      READ ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User BY \_UserDev
      ALL FIELDS WITH CORRESPONDING #( keys )
      RESULT DATA(lt_existing_UserDev).
*
      IF lt_existing_UserDev IS NOT INITIAL.
        MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
        ENTITY UserDev DELETE FROM VALUE #(
          FOR lwa_data IN lt_existing_UserDev (
            %key        = lwa_data-%key
          )
        )
        MAPPED DATA(lt_del_mapped)
        REPORTED DATA(lt_del_reported)
        FAILED DATA(lt_del_failed).
      ENDIF.
*

*      "Add New Entry for UserDev(association)
      MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User CREATE BY \_UserDev
      AUTO FILL CID WITH lt_data_user_dev
     MAPPED DATA(lt_userdev_mapped)
        REPORTED DATA(lt_userdev_reported)
        FAILED DATA(lt_userdev_failed).
      .
      IF lt_userdev_failed IS INITIAL.
        reported-%other = VALUE #( (  new_message_with_text( severity = if_abap_behv_message=>severity-success
                                                                         text = 'Excel Uploaded Successfully.' )
                                                                               ) ) .

      ENDIF.
*    "Modify Status
      MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User
      UPDATE FROM VALUE #(  (
          %tky        = lt_file_entity[ 1 ]-%tky
          FileStatus  = 'Excel Uploaded'
          %control-FileStatus = if_abap_behv=>mk-on ) )
      MAPPED DATA(lt_upd_mapped)
      FAILED DATA(lt_upd_failed)
      REPORTED DATA(lt_upd_reported).

      "Read Updated Entry
      READ ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User ALL FIELDS WITH CORRESPONDING  #( keys )
      RESULT DATA(lt_updated_User).

      "Send Status back to front end
      result = VALUE #(
        FOR lwa_upd_head IN lt_updated_User (
          %tky    = lwa_upd_head-%tky
          %param  = lwa_upd_head

        )
      ).
    ENDIF.


  ENDMETHOD.

  METHOD get_instance_features.
    READ ENTITIES OF zi_ati_user IN LOCAL MODE ENTITY User
    FIELDS ( EmpId DevId FileStatus TemplateStatus ) WITH CORRESPONDING #( keys )
    RESULT DATA(lt_users) FAILED failed.

"XAAYDIN 29.12.2025 Comment
*    result = VALUE #( FOR user IN lt_users
*                      LET uploadBtn = COND #( WHEN user-FileStatus = 'File Selected'
*                                             THEN if_abap_behv=>fc-o-enabled
*                                             ELSE if_abap_behv=>fc-o-disabled )
*
*                         DownloadTemplate = COND #( WHEN user-TemplateStatus = 'Absent'
*                                             THEN if_abap_behv=>fc-o-enabled
*                                             ELSE if_abap_behv=>fc-o-disabled )
*                      IN
*                                            ( %tky = user-%tky
*                                             %assoc-_UserDev = if_abap_behv=>fc-o-disabled
*                                             %action-uploadExcelData = uploadBtn
*                                             %action-DownloadExcel = DownloadTemplate
*                                            ) ).

  ENDMETHOD.

  METHOD FillSelectedStatus.

    "Read user Entities and change file status
    READ ENTITIES OF zi_ati_user IN LOCAL MODE
    ENTITY User ALL FIELDS WITH CORRESPONDING #( keys )
    RESULT DATA(lt_User).

    "Update File Status
    LOOP AT lt_User INTO DATA(ls_user).
      MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User
      UPDATE FIELDS ( FileStatus  )
      WITH VALUE #( (
          %tky                  = ls_user-%tky
          %data-FileStatus      = COND #(
                                    WHEN ls_user-Attachment IS INITIAL
                                    THEN 'File not Selected'
                                    ELSE 'File Selected' )
          %control-FileStatus   = if_abap_behv=>mk-on
          ) ).
    ENDLOOP.
*
    READ ENTITIES OF zi_ati_user IN LOCAL MODE
  ENTITY User ALL FIELDS WITH CORRESPONDING #( keys )
  RESULT DATA(lt_User_updated).
    "Update template Status
    LOOP AT lt_User_updated INTO DATA(ls_user_updated).
      MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User
      UPDATE FIELDS ( TemplateStatus  )
      WITH VALUE #( (
                    %tky                = ls_user-%tky
                    %data-TemplateStatus = COND #(
                                    WHEN ls_user-Attachment IS NOT INITIAL
                                    THEN COND #( WHEN ls_user-FileStatus = 'File Selected' THEN ' ' )
                                    ELSE 'Absent'

                                     )
          %control-TemplateStatus   = if_abap_behv=>mk-on

          ) ).
    ENDLOOP.

  ENDMETHOD.

  METHOD FillFileStatus.
    "Read the data to be modified
    READ ENTITIES OF zi_ati_user IN LOCAL MODE
    ENTITY User FIELDS ( EmpId DevId FileStatus )
    WITH CORRESPONDING #( keys )
    RESULT DATA(lt_user).

    "Update File Status
    LOOP AT lt_user INTO DATA(ls_user).
      MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User
      UPDATE FIELDS ( FileStatus TemplateStatus )
      WITH VALUE #( (
          %tky                  = ls_user-%tky
          %data-FileStatus      = 'File not Selected'
          %data-TemplateStatus      = 'Absent'
          %control-FileStatus   = if_abap_behv=>mk-on
           %control-TemplateStatus   = if_abap_behv=>mk-on
          ) ).
    ENDLOOP.

  ENDMETHOD.

  METHOD DownloadExcel.
    DATA: lv_file_content        TYPE zt2123_user-attachment .

    me->read_excel(
      EXPORTING
        im_get_template = 'X'
      CHANGING
        ch_attachment   = lv_file_content
    ).
*  "Modify Root Entity
    MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
    ENTITY User
    UPDATE FROM VALUE #( FOR ls_key IN keys
       (
       EmpId      = ls_key-EmpId
       DevId      = ls_key-DevId
       Attachment = lv_file_content
       Filename   = 'template.xlsx'
       Mimetype   = 'application/vnd.ms-excel'
       %control-Attachment  = if_abap_behv=>mk-on
       %control-Filename   = if_abap_behv=>mk-on
       %control-Mimetype  = if_abap_behv=>mk-on
       ) )
    MAPPED DATA(ls_mapped_update)
    REPORTED DATA(ls_reported_update)
    FAILED DATA(ls_failed_update).

*    "Read Updated Entry
    READ ENTITIES OF zi_ati_user IN LOCAL MODE
    ENTITY  User
    ALL FIELDS WITH
    CORRESPONDING #( Keys )
    RESULT DATA(lt_User).
*    "Update File Status
    LOOP AT lt_User INTO DATA(ls_user).
      MODIFY ENTITIES OF zi_ati_user IN LOCAL MODE
      ENTITY User
      UPDATE FIELDS ( FileStatus TemplateStatus )
      WITH VALUE #( (
          %tky                  = ls_user-%tky
          %data-FileStatus      = 'File not Selected'
          %data-TemplateStatus      = 'Present'
          %control-FileStatus   = if_abap_behv=>mk-on
          %control-TemplateStatus   = if_abap_behv=>mk-on
          ) )
          MAPPED DATA(ls_mapped_status)
    REPORTED DATA(ls_reported_status)
    FAILED DATA(ls_failed_status).
    ENDLOOP.

    "Send Status back to front end
    result = VALUE #( FOR ls_upd_user IN lt_User
                      ( %tky   = ls_upd_user-%tky
                        %param = ls_upd_user

                        ) ).
    IF ls_failed_update IS INITIAL.
      reported = VALUE #( BASE reported user = VALUE #( ( %tky = keys[ 1 ]-%tky
                                                         %msg = new_message_with_text( severity =
                                                         if_abap_behv_message=>severity-success
                                                         text = 'Template Available.' )

                                                                    ) )  ).
    ENDIF.
  ENDMETHOD.

  METHOD read_excel.
    DATA: lt_excel        TYPE STANDARD TABLE OF zbp_i_ati_user=>ty_exl_file,
          lo_worksheet_ra TYPE REF TO if_xco_xlsx_ra_worksheet, " for reading attachment with some content
          lo_worksheet_wa TYPE REF TO if_xco_xlsx_wa_worksheet " for reading blank attachment
          .
    "when im_get_template is 'X' then we want to download excel template else get excel data
    "when im_get_header is 'X then we want to get the data in internal table with header row

    DATA(lv_row) = COND #( WHEN im_get_template = 'X' THEN 1 ELSE 2 ).

    IF lv_row = 1.
      lt_excel = VALUE #( (
       emp_id = 'User Id'
       dev_id = 'Development Id'
       dev_desc = 'Development Description'
       obj_type = 'Object Type'
       obj_name = 'Object Name'
       ) ).
    ENDIF.

*    "Move Excel Data to Internal Table
    IF lv_row = 2.
      DATA(lo_xlsx)      = xco_cp_xlsx=>document->for_file_content( iv_file_content = ch_attachment
                                                                   )->read_access( ).
      lo_worksheet_ra = lo_xlsx->get_workbook( )->worksheet->at_position( 1 ).
    ELSE.
      "get excel template
      DATA(lo_write_access) = xco_cp_xlsx=>document->empty( )->write_access( ).
      lo_worksheet_wa = lo_write_access->get_workbook(
                                                  )->worksheet->at_position( lv_row ).
    ENDIF.


    DATA(lo_selection_pattern) = xco_cp_xlsx_selection=>pattern_builder->simple_from_to(
                                 )->from_row( xco_cp_xlsx=>coordinate->for_numeric_value(
                                              COND #( WHEN im_get_header = 'X' THEN 1 ELSE lv_row ) )
                                 )->get_pattern(  ).
    IF lv_row = 2.
      DATA(lo_execute) = lo_worksheet_ra->select( lo_selection_pattern
                                                )->row_stream(
                                                )->operation->write_to( REF #( lt_excel )
                                                ).

      lo_execute->set_value_transformation( xco_cp_xlsx_read_access=>value_transformation->string_value
                                            )->if_xco_xlsx_ra_operation~execute( ).
      et_excel_data = lt_excel.
    ELSE.
      lo_worksheet_wa->select( lo_selection_pattern
      )->row_stream(
      )->operation->write_from( REF #( lt_excel )
      )->execute( ).

      ch_attachment = lo_write_access->get_file_content( ).

    ENDIF.

  ENDMETHOD.

ENDCLASS.
