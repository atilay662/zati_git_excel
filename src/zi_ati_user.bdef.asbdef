managed implementation in class ZBP_I_ATI_USER unique;
strict ( 2 );
with draft;
define behavior for zi_ati_user alias User
persistent table zati_user
draft table zati_user_d
lock master total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create ( authorization : global );
  update;
  delete;
  field ( readonly : update ) EmpId, DevId;
  field ( readonly ) LocalCreatedBy, LocalLastChangedAt;
  field ( mandatory : create ) EmpId, DevId;
  association _UserDev { create ( features : instance ); with draft; }
  // Logic to read excel data into associated entity zi_ati_user_Dev
  action ( features : instance ) uploadExcelData result [1] $self;
  // Logic to download excel template
   action ( features : instance ) DownloadExcel result [1] $self;
  // Change File Status During Creation of new record
  determination FillFileStatus on modify { field EmpId, DevId; }
  // Change File Status When file is selected
  determination FillSelectedStatus on modify { field Attachment; }
  // copy existing row
//  factory action copyRecord [1]; // by default its instance bound
  // for reloading ui after data has been uploaded
  side effects
  {
    field Attachment affects field FileStatus;
    action uploadExcelData affects $self, field _UserDev.*;
  }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  mapping for zati_user
    {
      EmpId              = emp_id;
      DevId              = dev_id;
      DevDescription     = dev_description;
      Filename           = filename;
      Attachment         = attachment;
      Mimetype           = mimetype;
      FileStatus         = file_status;
      TemplateStatus     = template_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
      LocalLastChangedBy = local_last_changed_by;
    }
}

define behavior for zi_ati_user_dev alias UserDev
persistent table zati_user_dev
draft table zati_userdev_d
lock dependent by _User
authorization dependent by _User
//etag master
{
  update;
  delete;
  field ( readonly ) EmpId, DevId, SerialNo;
  association _User { with draft; }


  mapping for zati_user_dev
    {
      EmpId      = emp_id;
      DevId      = dev_id;
      SerialNo   = serial_no;
      ObjectType = object_type;
      ObjectName = object_name;
    }
}